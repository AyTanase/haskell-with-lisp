#?(defoperator |ratio| :op |:%|)

(extension FlexibleInstances
           MonoLocalBinds
           MultiParamTypeClasses
           UndecidableInstances)

(defpackage Ratio
  (:shadow Prelude negate + - *)
  (:use (:q Prelude) Classes)
  (:export (Ratio :all) rpure))


(data (Ratio a) (ratio a a) Show)

(instance (Eq (Ratio a)) ((Eq a) (Action a a))
  ((= (ratio u v) (ratio x y)) (= (* u y) (* v x))))


(type rpure (=> (Ring r) (-> r (Ratio r))))
(define rpure (flip ratio unit))

(type rmap (-> (-> a a) (Ratio a) (Ratio a)))
(define (rmap f (ratio x y)) (ratio (f x) y))

(instance (Group (Ratio r)) (:overlapping (Ring r))
  (zero (rpure zero))
  (negate (rmap negate))
  ((+ (ratio u v) (ratio x y))
    (ratio (+ (* u y) (* v x)) (* v y))))


(instance (Action a (Ratio b)) (:overlapping (Action a b))
  (* (compose rmap *)))

(instance (Action (Ratio a) (Ratio b))
    (:overlapping (Action a b))
  ((* (ratio u v) (ratio x y)) (ratio (* u x) (* v y))))


(instance (Ring (Ratio r)) (:overlapping (Ring r))
  (unit (rpure unit)))


(instance (Div a (Ratio b)) (:overlapping (Action a b))
  ((/ (ratio x y) z) (ratio x (* z y))))

(instance (Div (Ratio a) (Ratio b))
    (:overlapping (Action a b))
  ((/ r (ratio x y)) (* (ratio y x) r)))


(instance (Num (Ratio a)) ((Num a))
  (+ +) (- -) (* *)
  (negate negate)
  ((abs (ratio x y)) (ratio (abs x) (abs y)))
  ((signum (ratio x y)) (rpure (* (signum x) (signum y))))
  (fromInteger (compose rpure fromInteger)))
